plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 30

    defaultConfig {
        minSdk 21
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.websitebeaver:opencv:4.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "com.church1ll"
            artifactId = "documentscannermod"
            version = "1.0.0"

            afterEvaluate {
                from components.release

                pom {
                    name = 'Document Scanner Mod'
                    description = 'Document Scanner Mod'
                    url = 'https://github.com/Church1ll/doc-scan-android'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'church1ll'
                            name = 'Winston Church1ll'
                            email = 'cristian.musteata.e@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:Church1ll/doc-scan-android.git'
                        developerConnection = 'scm:git@github.com:Church1ll/doc-scan-android.git'
                        url = 'https://github.com/Church1ll/doc-scan-android'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Church1ll/doc-scan-android")
            credentials {
                username = 'xxx' // Github username
                password = 'xxx' //generate personal access token (classic) with write access
            }
        }
    }
}
